// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // a: 000, b: 001, c: 010, d: 011, e: 100, f: 101, g: 110, h: 111
    Mux8Way16(a=registerOut, b=registerIncOut, c=in, d=in, e=false, f=false, g=false, h=false, sel[0]=inc, sel[1]=load, sel[2]=reset, out=registerIn);
    Or(a=inc, b=reset, out=temp);
    Or(a=temp, b=load, out=loadIn);
    Register(in=registerIn, load=loadIn, out=out, out[0..15]=registerOut);
    Inc16(in=registerOut, out=registerIncOut);
}
